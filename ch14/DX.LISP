(DE DERIV (F)
  (COND
    ((EQ F 'X) '1)
    ((ATOM F) '0)
    ((EQ (CAR F) 'PLUS) (LIST 'PLUS (DERIV (CADR F))
                                    (DERIV (CADDR F))))
    ((EQ (CAR F) 'DIFFERENCE) (LIST 'DIFFERENCE (DERIV (CADR F))
                                    (DERIV (CADDR F))))
    ((EQ (CAR F) 'TIMES) (LIST 'PLUS (LIST 'TIMES (DERIV (CADR F))
                                                  (CADDR F))
                                     (LIST 'TIMES (CADR F)
                                           (DERIV (CADDR F)))))
    ((EQ (CAR F) 'QUOTIENT)
         (LIST 'QUOTIENT (LIST 'DIFFERENCE
                              (LIST 'TIMES (DERIV (CADR F))
                                           (CADDR F))
                              (LIST 'TIMES (DERIV (CADDR F))
                                           (CADR F)))
                         (LIST 'TIMES (CADDR F) (CADDR F))))

    ((EQ (CAR F) 'SIN) (LIST 'TIMES (LIST 'COS (CADR F))
                                               (DERIV (CADR F))))
    ((EQ (CAR F) 'COS) (LIST 'TIMES (LIST 'MINUS (LIST 'SIN (CADR F)))
                                                 (DERIV (CADR F))))
    ((EQ (CAR F) 'TAN) (LIST 'TIMES (LIST 'SEC**2 (CADR F))
                                                 (DERIV (CADR F))))
    ((EQ (CAR F) 'EXP) (LIST 'TIMES (LIST 'EXP (CADR F))
                                               (DERIV (CADR F))))
    ((EQ (CAR F) 'LN) (LIST 'QUOTIENT (DERIV (CADR F)) (CADR F)))
    ((EQ (CAR F) 'MINUS) (LIST 'MINUS (DERIV (CADR F))))
    ((EQ (CAR F) 'POWER) (LIST 'TIMES (LIST 'TIMES (CADDR F)
                                      (LIST 'POWER (CADR F)
                                             (DIFFERENCE (CADDR F) 1)))
                                      (DERIV (CADR F))))
    (T (LIST 'Syntax 'Error 'From (CAR F)))
    ))

(DE SIMPLIFY (F)
  (COND
    ((ATOM F) F)
    ((AND (EQ (CAR F) 'PLUS)
        (EQ (SIMPLIFY (CADR F)) '0)) (SIMPLIFY (CADDR F)))
    ((AND (EQ (CAR F) 'PLUS)
        (EQ (SIMPLIFY (CADDR F)) '0)) (SIMPLIFY (CADR F)))
    ((EQ (CAR F) 'PLUS) (LIST 'PLUS (SIMPLIFY (CADR F))
                                    (SIMPLIFY (CADDR F))))

    ((AND (EQ (CAR F) 'TIMES)
        (EQ (SIMPLIFY (CADDR F)) '0)) '0)
    ((AND (EQ (CAR F) 'TIMES)
        (EQ (SIMPLIFY (CADR F)) '0)) '0)
    ((AND (EQ (CAR F) 'TIMES)
        (EQ (SIMPLIFY (CADR F)) '1)) (SIMPLIFY (CADDR F)))
    ((AND (EQ (CAR F) 'TIMES)
        (EQ (SIMPLIFY (CADDR F)) '1)) (SIMPLIFY (CADR F)))
    ((EQ (CAR F) 'TIMES) (LIST 'TIMES (SIMPLIFY (CADR F))
                                     (SIMPLIFY (CADDR F))))

    ((AND (EQ (CAR F) 'DIFFERENCE)
        (EQ (SIMPLIFY (CADR F)) '0)) (LIST 'MINUS (SIMPLIFY (CADDR F))))
    ((AND (EQ (CAR F) 'DIFFERENCE)
        (EQ (SIMPLIFY (CADDR F)) '0)) (SIMPLIFY (CADR F)))
    ((EQ (CAR F) 'DIFFERENCE) (LIST 'DIFFERENCE (SIMPLIFY (CADR F))
                                               (SIMPLIFY (CADDR F))))

    ((EQ (CAR F) 'MINUS) (LIST 'MINUS (SIMPLIFY (CADR F))))
    ((EQ (CAR F) 'QUOTIENT) (LIST 'QUOTIENT (SIMPLIFY (CADR F))
                                           (SIMPLIFY (CADDR F))))
    (T F)
))

(DE INFIX (F)
  (COND
    ((ATOM F) F)
    ((EQ (CAR F) 'PLUS)
         (LIST (INFIX (CADR F)) '+ (INFIX (CADDR F))))
    ((EQ (CAR F) 'DIFFERENCE)
         (LIST (INFIX (CADR F)) '- (INFIX (CADDR F))))
    ((EQ (CAR F) 'TIMES)
         (LIST (INFIX (CADR F)) '* (INFIX (CADDR F))))
    ((EQ (CAR F) 'QUOTIENT)
         (LIST (INFIX (CADR F)) '/ (INFIX (CADDR F))))
    ((EQ (CAR F) 'MINUS)
         (LIST '- (INFIX (CADR F))))
    ((EQ (CAR F) 'SIN)
         (LIST 'SIN (INFIX (CADR F))))
    ((EQ (CAR F) 'COS)
         (LIST 'COS (INFIX (CADR F))))
    ((EQ (CAR F) 'TAN)
         (LIST 'TAN (INFIX (CADR F))))
    ((EQ (CAR F) 'SEC**2)
         (LIST 'SEC**2 (INFIX (CADR F))))
    ((EQ (CAR F) 'EXP)
         (LIST 'EXP (INFIX (CADR F))))
    ((EQ (CAR F) 'LN)
         (LIST 'LN (INFIX (CADR F))))
    ((EQ (CAR F) 'POWER)
         (LIST (INFIX (CADR F)) '** (CADDR F)))
    (T (LIST 'Syntax 'Error 'From (CAR F)))
))

(DE DX (F)
    (INFIX (SIMPLIFY (DERIV F))))

